apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.gms.google-services'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.wstik.kinde"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode getVersionCode(versionMajor, versionMinor, versionPatch, versionBuild)
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        applicationVariants.all { variant ->
            renameAPK(variant, defaultConfig)
        }
    }
    signingConfigs {
        staging {
            keyAlias keystoreProperties['stagingKeyAlias']
            keyPassword keystoreProperties['stagingKeyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        prod {
            keyAlias keystoreProperties['prodKeyAlias']
            keyPassword keystoreProperties['prodKeyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug{
            signingConfig null
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "server"
    productFlavors {
        staging {
            dimension "server"
            signingConfig signingConfigs.staging
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }
        prod {
            dimension "server"
            signingConfig signingConfigs.prod
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

static def getVersionCode(versionMajor, versionMinor, versionPatch, versionBuild) {
    return versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
}

static def renameAPK(variant, defaultConfig) {
    variant.outputs.all { output ->
        outputFileName = "kinde_v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ").apk"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.2.0'

    //Firebase
    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-auth:19.3.0'
    implementation 'com.google.android.gms:play-services-auth:18.0.0'

    //Dependency Injection
    implementation 'org.koin:koin-android:2.0.1'
    implementation 'org.koin:koin-androidx-scope:2.0.1'
    implementation 'org.koin:koin-androidx-viewmodel:2.0.1'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp-integration:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    //ReactiveX
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'android.arch.core:core-testing:1.1.1'
}
